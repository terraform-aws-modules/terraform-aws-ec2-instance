provider "aws" {
  region = local.region
}

data "aws_availability_zones" "available" {}

locals {
  name   = "ex-${basename(path.cwd)}"
  region = "eu-west-1"

  vpc_cidr = "10.0.0.0/16"
  azs      = slice(data.aws_availability_zones.available.names, 0, 3)

  user_data = <<-EOT
    #!/bin/bash
    echo "Hello Terraform!"
  EOT

  tags = {
    Name       = local.name
    Example    = local.name
    Repository = "https://github.com/terraform-aws-modules/terraform-aws-ec2-instance"
  }
}

################################################################################
# EC2 Module
################################################################################

module "ec2_complete" {
  source = "../../"

  name = local.name

  ami                    = data.aws_ami.amazon_linux.id
  instance_type          = "c5.xlarge" # used to set core count below
  availability_zone      = element(module.vpc.azs, 0)
  subnet_id              = element(module.vpc.private_subnets, 0)
  vpc_security_group_ids = [module.security_group.security_group_id]
  placement_group        = aws_placement_group.web.id
  create_eip             = true
  disable_api_stop       = false

  create_iam_instance_profile = true
  iam_role_description        = "IAM role for EC2 instance"
  iam_role_policies = {
    AdministratorAccess = "arn:aws:iam::aws:policy/AdministratorAccess"
  }

  # only one of these can be enabled at a time
  hibernation = true
  # enclave_options_enabled = true

  user_data_base64            = base64encode(local.user_data)
  user_data_replace_on_change = false

  cpu_options = {
    core_count       = 2
    threads_per_core = 1
  }
  enable_volume_tags = false
  root_block_device = {
    encrypted  = true
    type       = "gp3"
    throughput = 200
    size       = 50
    tags = {
      Name = "my-root-block"
    }
  }

  ebs_volumes = {
    "/dev/sdf" = {
      size       = 5
      throughput = 200
      encrypted  = true
      kms_key_id = aws_kms_key.this.arn
      tags = {
        MountPoint = "/mnt/data"
      }
    }
  }

  tags = local.tags
}

module "ec2_network_interface" {
  source = "../../"

  name = "${local.name}-network-interface"

  network_interface = {
    0 = {
      network_interface_id  = aws_network_interface.this.id
      delete_on_termination = false
    }
  }

  tags = local.tags
}

module "ec2_metadata_options" {
  source = "../../"

  name = "${local.name}-metadata-options"

  subnet_id = element(module.vpc.private_subnets, 0)

  metadata_options = {
    http_endpoint               = "enabled"
    http_tokens                 = "required"
    http_put_response_hop_limit = 8
    instance_metadata_tags      = "enabled"
  }

  tags = local.tags
}

module "ec2_t2_unlimited" {
  source = "../../"

  name = "${local.name}-t2-unlimited"

  instance_type               = "t2.micro"
  cpu_credits                 = "unlimited"
  subnet_id                   = element(module.vpc.private_subnets, 0)
  associate_public_ip_address = true

  maintenance_options = {
    auto_recovery = "default"
  }

  tags = local.tags
}

module "ec2_t3_unlimited" {
  source = "../../"

  name = "${local.name}-t3-unlimited"

  instance_type               = "t3.micro"
  cpu_credits                 = "unlimited"
  subnet_id                   = element(module.vpc.private_subnets, 0)
  associate_public_ip_address = true

  tags = local.tags
}

module "ec2_disabled" {
  source = "../../"

  create = false
}

################################################################################
# EC2 Module - with ignore AMI changes
################################################################################

module "ec2_ignore_ami_changes" {
  source = "../../"

  name = "${local.name}-ignore-ami-changes"

  ignore_ami_changes = true

  ami               = data.aws_ami.amazon_linux.id
  instance_type     = "t2.micro"
  availability_zone = element(module.vpc.azs, 0)
  subnet_id         = element(module.vpc.private_subnets, 0)

  tags = local.tags
}

################################################################################
# EC2 Module - multiple instances with `for_each`
################################################################################

locals {
  multiple_instances = {
    one = {
      instance_type     = "t3.micro"
      availability_zone = element(module.vpc.azs, 0)
      subnet_id         = element(module.vpc.private_subnets, 0)
      root_block_device = {
        encrypted  = true
        type       = "gp3"
        throughput = 200
        size       = 50
        tags = {
          Name = "my-root-block"
        }
      }
    }
    two = {
      instance_type     = "t3.small"
      availability_zone = element(module.vpc.azs, 1)
      subnet_id         = element(module.vpc.private_subnets, 1)
      root_block_device = {
        encrypted = true
        type      = "gp2"
        size      = 50
      }
    }
    three = {
      instance_type     = "t3.medium"
      availability_zone = element(module.vpc.azs, 2)
      subnet_id         = element(module.vpc.private_subnets, 2)
    }
  }
}

module "ec2_multiple" {
  source = "../../"

  for_each = local.multiple_instances

  name = "${local.name}-multi-${each.key}"

  instance_type     = each.value.instance_type
  availability_zone = each.value.availability_zone
  subnet_id         = each.value.subnet_id

  enable_volume_tags = false
  root_block_device  = try(each.value.root_block_device, null)

  tags = local.tags
}

################################################################################
# EC2 Module - spot instance request
################################################################################

module "ec2_spot_instance" {
  source = "../../"

  name                 = "${local.name}-spot-instance"
  create_spot_instance = true

  availability_zone           = element(module.vpc.azs, 0)
  subnet_id                   = element(module.vpc.private_subnets, 0)
  vpc_security_group_ids      = [module.security_group.security_group_id]
  associate_public_ip_address = true

  # Spot request specific attributes
  spot_price                = "0.1"
  spot_wait_for_fulfillment = true
  spot_type                 = "persistent"
  # End spot request specific attributes

  user_data_base64 = base64encode(local.user_data)

  cpu_options = {
    core_count       = 2
    threads_per_core = 1
  }

  enable_volume_tags = false
  root_block_device = {
    encrypted  = true
    type       = "gp3"
    throughput = 200
    size       = 50
    tags = {
      Name = "my-root-block"
    }
  }

  ebs_volumes = {
    "/dev/sdf" = {
      size       = 5
      throughput = 200
      encrypted  = true
      # kms_key_id  = aws_kms_key.this.arn # you must grant the AWSServiceRoleForEC2Spot service-linked role access to any custom KMS keys
    }
  }

  tags = local.tags
}

################################################################################
# EC2 Module - Capacity Reservation
################################################################################

module "ec2_open_capacity_reservation" {
  source = "../../"

  name = "${local.name}-open-capacity-reservation"

  instance_type               = "m4.large"
  subnet_id                   = element(module.vpc.private_subnets, 0)
  associate_public_ip_address = false

  capacity_reservation_specification = {
    capacity_reservation_target = {
      capacity_reservation_id = aws_ec2_capacity_reservation.open.id
    }
  }

  tags = local.tags
}

module "ec2_targeted_capacity_reservation" {
  source = "../../"

  name = "${local.name}-targeted-capacity-reservation"

  instance_type               = "m4.large"
  subnet_id                   = element(module.vpc.private_subnets, 0)
  associate_public_ip_address = false

  capacity_reservation_specification = {
    capacity_reservation_target = {
      capacity_reservation_id = aws_ec2_capacity_reservation.targeted.id
    }
  }

  tags = local.tags
}

resource "aws_ec2_capacity_reservation" "open" {
  instance_type           = "m4.large"
  instance_platform       = "Linux/UNIX"
  availability_zone       = "${local.region}a"
  instance_count          = 1
  instance_match_criteria = "open"
}

resource "aws_ec2_capacity_reservation" "targeted" {
  instance_type           = "m4.large"
  instance_platform       = "Linux/UNIX"
  availability_zone       = "${local.region}a"
  instance_count          = 1
  instance_match_criteria = "targeted"
}

################################################################################
# Supporting Resources
################################################################################

module "vpc" {
  source  = "terraform-aws-modules/vpc/aws"
  version = "~> 6.0"

  name = local.name
  cidr = local.vpc_cidr

  azs             = local.azs
  private_subnets = [for k, v in local.azs : cidrsubnet(local.vpc_cidr, 4, k)]
  public_subnets  = [for k, v in local.azs : cidrsubnet(local.vpc_cidr, 8, k + 48)]

  tags = local.tags
}

data "aws_ami" "amazon_linux" {
  most_recent = true
  owners      = ["amazon"]
  name_regex  = "^al2023-ami-2023.*-x86_64"
}

module "security_group" {
  source  = "terraform-aws-modules/security-group/aws"
  version = "~> 5.0"

  name        = local.name
  description = "Security group for example usage with EC2 instance"
  vpc_id      = module.vpc.vpc_id

  ingress_cidr_blocks = ["0.0.0.0/0"]
  ingress_rules       = ["http-80-tcp", "all-icmp"]

  tags = local.tags
}

resource "aws_placement_group" "web" {
  name     = local.name
  strategy = "cluster"
}

resource "aws_kms_key" "this" {
}

resource "aws_network_interface" "this" {
  subnet_id       = element(module.vpc.private_subnets, 0)
  security_groups = [module.security_group.security_group_id]
}
